



rule all:
   input:
      "data/intergenic_upstream_repeats.bed", 
      "data/intergenic_downstream_repeats.bed", 
      "plots/intergenic_heatmap.pdf",
      "plots/intergenic_violin_length.pdf",
      "plots/intergenic_violin_repeat.pdf"


rule get_genes_bed:
   input: "data/EnE101_chr_assembly_v3_genes_v01.agat.gff"
   output: "data/EnE101_chr_assembly_v3_onlygenes_v01.agat.bed"
   shell: """
      awk '$3=="gene"' {input} | sort -k1,1 -k4,4n | sed 's/ID=//g' | sed 's/;.*//g' | awk '{{print $1"\t"$4-1"\t"$5"\t"$9"\t.\t"$7}}' > {output}
   """

rule bed_complement:
   conda: "env.yml"
   input: 
      genome="data/EnE101_chr_assembly_v3_masked.txt", 
      bed="data/EnE101_chr_assembly_v3_onlygenes_v01.agat.bed"
   output: "data/EnE101_chr_assembly_v3_masked_complement.bed"
   shell: """
      sort -k1,1 {input.genome} | bedtools complement -i {input.bed} -g - > {output}
   """


rule get_intergenic_regions:
   conda: "env.yml"
   input: 
      genes="data/EnE101_chr_assembly_v3_onlygenes_v01.agat.bed", 
      complement="data/EnE101_chr_assembly_v3_masked_complement.bed"
   output: upstream="data/intergenic_upstream.bed", downstream="data/intergenic_downstream.bed"
   shell: """
      bedtools closest -a {input.genes} -b {input.complement} -t first -D a -id > {output.upstream}
      bedtools closest -a {input.genes} -b {input.complement} -t first -D a -iu > {output.downstream}
   """


rule plot_heatmap:
   conda: "env.yml"
   input: 
      upstream="data/intergenic_upstream.bed", 
      downstream="data/intergenic_downstream.bed", 
      genes_interest="data/gene_categories.txt"
   output: "plots/intergenic_heatmap.pdf"
   shell: """
      Rscript scripts/plot_intergenic_heatmap.R {input.upstream} {input.downstream} {input.genes_interest} {output}
   """



rule get_repeat_contente_intergenic:
   conda: "env.yml"
   input: 
      upstream="data/intergenic_upstream.bed", 
      downstream="data/intergenic_downstream.bed", 
      rm_gff="data/EnE101_chr_assembly_v3.fasta.out.gff"
   output:
      upstream="data/intergenic_upstream_repeats.bed", 
      downstream="data/intergenic_downstream_repeats.bed", 
   shell: """
      cat {input.upstream}   | awk '{{print $7"\t"$8"\t"$9"\t"$4}}'  | bedtools coverage -a - -b {input.rm_gff} > {output.upstream}
      cat {input.downstream} | awk '{{print $7"\t"$8"\t"$9"\t"$4}}'  | bedtools coverage -a - -b {input.rm_gff} > {output.downstream}
   """


rule plot_violin:
   conda: "env.yml"
   input: 
      upstream="data/intergenic_upstream_repeats.bed", 
      downstream="data/intergenic_downstream_repeats.bed", 
      genes_interest="data/gene_categories.txt"
   output: 
      intergenic_length = "plots/intergenic_violin_length.pdf",
      intergenic_repeat = "plots/intergenic_violin_repeat.pdf"
   shell: """
      Rscript scripts/plot_intergenic_violin.R {input.upstream} {input.downstream} {input.genes_interest} {output.intergenic_length} {output.intergenic_repeat}
   """

